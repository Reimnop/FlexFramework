#version 430
layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

const float PI = 3.1415926535897932384626433832795;
const float POSITIVE_INF = uintBitsToFloat(0x7F800000);

uniform sampler2D inputTexture;
uniform mat4 inverseProjection;
uniform mat4 inverseView;

layout(rgba16f, binding = 0) uniform image2D outputImage;
layout(rgba16f, binding = 1) uniform image2D outputNormalImage;
layout(rgba16f, binding = 2) uniform image2D outputPositionImage;

vec3 getCamDirection(vec2 uv) {
    vec4 pos = vec4(uv, 0.0, 1.0) * inverseProjection;
    return normalize(pos.xyz * mat3(inverseView));
}

vec4 samplePanorama(vec3 direction) {
    float theta = acos(direction.y) / PI;
    float phi = atan(direction.x, -direction.z) / -PI * 0.5;
    vec2 uv = vec2(phi, theta);
    return texture(inputTexture, uv);
}

void main() {
    ivec2 size = imageSize(outputImage);
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    vec2 pixelSize = 1.0 / vec2(size);
    vec2 texCoord = pixelCoord / vec2(size) + pixelSize * 0.5;

    vec3 direction = getCamDirection(texCoord * vec2(2.0) - vec2(1.0));
    vec4 color = samplePanorama(direction);
    imageStore(outputImage, pixelCoord, color);
    imageStore(outputNormalImage, pixelCoord, vec4(-direction, 1.0));
    imageStore(outputPositionImage, pixelCoord, vec4(direction * POSITIVE_INF, 1.0));
}